// This file returns the set of posts, ready for templating

// Load metadata from YAML.
// Upgrade dates to real Date objects.
// Add key as slug.
// Add URL.
// Add blog post text as `@text`.
@map(posts.yaml/, {
  value: (fields, key) => {
    ...fields
    slug: key
    url: `${ siteInfo.yaml/url }/blog/${ key }.html`
    pubDate: new:@js/Date(fields/pubDate)
    updatedDate: @if(fields/updatedDate, new:@js/Date(fields/updatedDate))
    @text: blog(`${key}.md`)
  }
})

// Convert blog posts to HTML
→ @mapFn(@mdHtml)

// Sort by date
→ @sortFn({ sortKey: (post) => post/pubDate })

// Reverse the order
→ @reverse

// Make this into a plain JavaScript object so it can be used anywhere
→ @plain
